(* syntatic definition *)
syntax
= syntax, syntax_factor
| syntax_factor;

syntax_factor
= comment | definition;

comment
= lcomment_symbol, comment_term, rcomment_symbol;

comment_term
= comment_term, comment_factor
| comment_factor;

comment_factor
= terminal_symbol - comment_symbol
| comment_term;

comment_symbol
= lcomment_symbol
| rcomment_symbol;

definition
= meta_identifier, defining_symbol, defining_list, terminator_symbol;

meta_identifier
= letter_symbol, meta_character
| letter_symbol;

meta_character
= meta_character, meta_factor
| meta_factor;

meta_factor
= letter_symbol
| digit_symbol
| '_'
| ;

defining_list
= defining, separator_symbol, defining
| defining;

defining
= defining_term, concatenator_symbol, defining_term
| defining_term;

defining_term
= defining_factor, exceptor_symbol, defining_factor
| defining_factor;

defining_factor
= terminal_string
| meta_identifier;

terminal_string
= first_quote_symbol, first_terminal_string, first_quote_symbol
| second_quote_symbol, second_terminal_string, second_quote_symbol;

first_terminal_string
= first_terminal_string, first_terminal_character
| first_terminal_character;

first_terminal_character
= terminal_symbol - first_quote_symbol;

second_terminal_string
= second_terminal_string, second_terminal_character
| second_terminal_character;

second_terminal_character
= terminal_symbol - second_quote_symbol;

(* lexical definition *)
digit_symbol
= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9';

letter_symbol
= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm'
| 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
| 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M'
| 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z';

defining_symbol
= '=';

separator_symbol
= '|';

concatenator_symbol
= ',';

exceptor_symbol
= '-';

terminator_symbol
= ';'; 

lcomment_symbol
= '(*';

rcomment_symbol
= '*)';

first_quote_symbol
= "'";

second_quote_symbol
= '"';

space_symbol
= ' ';

tabulator_symbol
= '	';

new_line_symbol
= '
';

other_symbol
= '~' | '~' | '@' | '#' | '$' | '%' | '^' | '&'
| '*' | '(' | ')' | '_' | '+' | '{' | '[' | '}'
| ']' | '\' | ':' | '<' | '>' | '.' | '?' | '/';

terminal_symbol
= digit_symbol
| letter_symbol
| defining_symbol
| separator_symbol
| concatenator_symbol
| exceptor_symbol
| terminator_symbol
| lcomment_symbol
| rcomment_symbol
| first_quote_symbol
| second_quote_symbol
| space_symbol
| tabulator_symbol
| new_line_symbol
| other_symbol;

